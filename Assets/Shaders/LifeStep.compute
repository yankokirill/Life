#pragma kernel LifeStep
#pragma target 5.0

RWTexture2D<float> Result;
Texture2D<float> State;

int _Width;
int _Height;

[numthreads(8, 8, 1)]
void LifeStep(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= _Width || id.y >= _Height)
        return;

    int2 uv = int2(id.x, id.y);
    float sum = 0.0;

    for (int oy = -1; oy <= 1; oy++)
    {
        for (int ox = -1; ox <= 1; ox++)
        {
            if (ox == 0 && oy == 0)
                continue;

            int2 n = uv + int2(ox, oy);
            
            if (n.x < 0)
                n.x = _Width - 1;
            else if (n.x >= _Width)
                n.x = 0;
            
            if (n.y < 0)
                n.y = _Height - 1;
            else if (n.y >= _Height)
                n.y = 0;

             sum += State[n];
        }
    }

    float self = State[uv];
    float newState = 0.0;

    if (self > 0.5)
    {
        if (sum == 2.0 || sum == 3.0)
            newState = 1.0;
    }
    else
    {
        if (sum == 3.0)
            newState = 1.0;
    }

    Result[uv] = newState;
}
